grammar com.telefonica.ietf.nemo.NemoDSL with org.eclipse.xtext.common.Terminals 
	
generate nemoDSL "http://www.telefonica.com/ietf/nemo/NemoDSL"

NemoFile:
	(server=ServerDef)?
	specss+=Spec*
;


ServerDef:
	// Not in draft
	("Engines:" | "ENGINES:") engines+=IP4 (',' engines+=IP4)* ';'
;
	
Spec:
	(Model | Create | Delete | Update | Import)
	';'
;

/*
 IMPORT Node internet Type ext-group  Property location:"openflow:3:4",ac-info-network:"layer3",ac-info-protocol:"static",ip-prefix:"172.168.1.0/24";
 IMPORT Node video-server2 Type host;
 */
Import:
	"IMPORT" "Node" name=ID 
	"Type" importType=[NodeModel]
	("Property" importProps+=[DefineProperty] ':' importVals+=STRING (',' importProps+=[DefineProperty] ':' importVals+=STRING)*)?	
;

Model : 
	ConnectionModel | NodeModel | LinkModel | FlowModel | ActionModel
;


ConnectionModel:
	"ConnectionModel" name=ID
	"Property" linkProperties += DefineProperty (',' linkProperties+=DefineProperty)* 
;
NodeModel:
	"NodeModel" name=ID
	"Property" nodeProperties += DefineProperty (',' nodeProperties+=DefineProperty)* 
;
LinkModel:
	"LinkModel" name=ID
	"Property" linkProperties += DefineProperty (',' linkProperties+=DefineProperty)* 
;
FlowModel:
	"FlowModel" name=ID
	"Property" flowProperties += DefineProperty (',' flowProperties+=DefineProperty)* 
;
ActionModel:
	"ActionModel" name=ID
	"Property" actionProperties += DefineProperty (',' actionProperties+=DefineProperty)* 
;

Create:
	"CREATE" (NodeCreate | LinkCreate | ConnectionCreate | FlowCreate | User)
;

// CREATE "user2" "abc" "tenant"; 
// Not in draft 
// *** Make sense??? ***
User: 
	userName=STRING password=STRING role=STRING
;

NodeCreate:
	"Node" name=ID 
		"Type"  nodeType=[NodeModel]
		("Contains" nodeContain+=[NodeCreate] (',' nodeContain+=[NodeCreate])*)?
		"Property" nodeProperties+=UseProperty (',' nodeProperties += UseProperty)*
;
LinkCreate:
	"Link" name=ID 
		"Type"  linkType=[LinkModel]
		"Property" linkProperties+=UseProperty (',' linkProperties += UseProperty)*
;

ConnectionCreate:
	"Connection" name=ID
	"Type" conectionType=[ConnectionModel]	
	("EndNodes" nodes+=[NodeCreate] ',' nodes+=[NodeCreate])?
	"Property" nodeProperties+=UseProperty (',' nodeProperties += UseProperty)*		
;

FlowCreate:
	"Flow" name=ID
	"Match" matchProp+=[DefineProperty] ':' ((val+=STRING) |
		("LIST" "(" listVal+=STRING (',' listVal+=STRING)")") |
		("RANGE" "(" lowerVal+=STRING ',' upperVal+=STRING))
		 (',' matchProp+=[DefineProperty] ':' ((val+=STRING) |
		("LIST" "(" listVal+=STRING (',' listVal+=STRING)")") |
		("RANGE" "(" lowerVal+=STRING ',' upperVal+=STRING)))	
;

Delete:
	"DELETE" (NodeDelete | LinkDelete)
;
LinkDelete:
	"Link" del=[LinkCreate]
;
NodeDelete:
	"Node" del=[NodeCreate]	
;

Update:
	"UPDATE" (UpdateNode|UpdateConnection|UpdateFlow)
;

UpdateNode:
	"Node" node=[NodeCreate]
		("Contains" nodeContain+=[NodeCreate] (',' nodeContain+=[NodeCreate])*)?
		"Property" nodeProperties+=UseProperty (',' nodeProperties += UseProperty)*	
;

UpdateConnection:
	"Connection" modConnection=[ConnectionCreate]
	("EndNodes" nodes+=[NodeCreate] ',' nodes+=[NodeCreate])?
	("Property" nodeProperties+=UseProperty (',' nodeProperties += UseProperty)*)?		
;

/*
 *    <flow_update> := UPDATE Flow <flow_id>
                           Match {<property_name>: <value>
                                 | Range (<value>, <value>)
                                 | List({<value>})}
 
 */
UpdateFlow:
	"Flow" flowID=[FlowCreate]
	"Match" matchProps+=[DefineProperty] ':' matchValues+=FlowValue
		 (',' matchProps+=[DefineProperty] ':' matchValues+=FlowCreate)*	
;
DefineProperty:
	propertyType=Type ':' name=ID
;

UseProperty:
	definedProp=[DefineProperty] ':' propValue=STRING
;

FlowProperty:
	flowProp=[DefineProperty] ':' flowvalue=FlowValue
;
Type:
	(atomicType=AtomicType) | 
	('List' '(' typeOfList=AtomicType ')')
;

AtomicType:
	{AtomicType} "Boolean" | "Integer" | "String" | "Date" | "UUID" | "EthAddr" | "IPPrefix"
;

FlowValue:
	(val=STRING) |
	("LIST" "(" listVal+=STRING (',' listVal+=STRING)")") |
	("RANGE" "(" lowerVal+=STRING ',' upperVal+=STRING)
;

terminal IP4: INT '.' INT '.' INT '.' INT;
